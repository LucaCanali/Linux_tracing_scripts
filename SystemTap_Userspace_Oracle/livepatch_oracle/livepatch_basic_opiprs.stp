#!/usr/local/bin/stap
#
# This is a script for live patching Oracle SQL parsing
#    1. The Oracle probe on opiprs catches all the hard parsing of SQL
#    2. The SQL statement matched against TARGET_SQL
#    3. If a match is found the SQL is replaced with REPLACEMENT_SQL
# This can be useful in the context of security band aid or performance tuning  
# The probe only works if REPLACEMENT_SQL is not longer than TARGET_SQL
#
# This is example code, not intended to be used in production
# Requires SystemTap guru mode as it overwrites memory
#
# Use: stap -g -v livepatch_basic_opiprs.stp
# Note: optionally add -x <pid> to limit the action to <pid> only (default is for all oracle sessions)
#
# Oracle binary is expected to be in the PATH: export PATH=$PATH:$ORACLE_HOME/bin
# run as priviledge user (root)
#
# Note: this is a simplified version of livepatch_opiprs.stp
#
# Compatibility: tested for Oracle 11.2.0.4 on RHEL/OL 6 and 7
#                tested for Oracle 12.1.0.2 on RHEL/OL 6 and 7 with kernel up to 3.10.0-123.x
#                issue: uprobes for kernel higher than 3.10.0-123.x will not work on Oracle 12c
#				 SystemTap version 2.5 or higher. Kernel debuginfo not needed for this probe.
#
# Author: Luca.Canali@cern.ch
# Created: Nov 2014, Last updated: Feb 2016
#

%{
/* SQL that will replace TARGET_SQL */
#define REPLACEMENT_SQL "select power(count(*),3) from dba_objects"
%}

global TARGET_SQL = "select count(*) from dba_objects, dba_objects, dba_objects"

function replace_SQL(pointersql:long) %{
    char *sqltext;

    sqltext = (char *) STAP_ARG_pointersql;
    /* This changes in memory (stack) the SQL text that will be parsed */
    strcpy(sqltext, "select power(count(*),3) from dba_objects");
%}

probe process("oracle").function("opiprs") {
    sqltext = user_string2(register("rsi"),"error")  
    # debug code
    # sqllength = register("rdx")
    # printf("opiParse: arg2=%s, arg3=%d\n",sqltext,sqllength)
    if (sqltext == TARGET_SQL) {
        printf("FOUND!\n") # debug code
        replace_SQL(register("rsi"))
    }
}


